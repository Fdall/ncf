#####################################################################################
# Copyright 2013 Normation SAS
#####################################################################################
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, Version 3.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#
#####################################################################################

# @name Permissions type recursion
# @description Ensure that a file or directory is present and has the right mode/owner/group
#
# @parameter path      Path to edit
# @parameter mode      Mode of the path to edit
# @parameter owner     Owner of the path to edit
# @parameter group     Group of the path to edit
# @parameter type      Type of the path to edit (all/files/directories)
# @parameter recursion Recursion depth to enforce for this path (0, 1, 2, ..., inf)
#
# @class_prefix permissions
# @class_parameter path

bundle agent permissions_type_recursion(path, mode, owner, group, type, recursion)
{
  vars:
      "old_class_prefix"        string => canonify("permissions_${path}");
      "uniq_old_class_prefix"   string => canonify("${report_data.directive_id}_permissions_${path}");
      "args"                     slist => { "${path}", "${mode}", "${owner}", "${group}", "${type}", "${recursion}" };
      "report_param"            string => join("_", args);
      "full_class_prefix"       string => canonify("permissions_${report_param}");
      "class_prefix"            string => string_head("${full_class_prefix}", "1000");
      "uniq_full_class_prefix"  string => canonify("${report_data.directive_id}_${full_class_prefix}");
      "uniq_class_prefix"       string => string_head("${uniq_full_class_prefix}", "1000");

  classes:
      "is_type_all"         expression => strcmp("${type}", "all");
      "is_type_files"       expression => strcmp("${type}", "files");
      "is_type_directories" expression => strcmp("${type}", "directories");

      # Workaround for https://dev.cfengine.com/issues/7310
      "is_depth_zero"       expression => strcmp("${recursion}", "0");

      "target_exists"       expression => fileexists("${path}");
      "is_target_directory" expression => isdir("${path}");

  files:

    is_type_all.is_target_directory.!is_depth_zero::

      "${path}"
        create       => "false",
        perms        => mog("${mode}", "${owner}", "${group}"),
        depth_search => recurse_with_base("${recursion}"),
        classes      => classes_generic_two("${uniq_old_class_prefix}", "${uniq_class_prefix}");

    is_type_all.((is_target_directory.is_depth_zero)|!is_target_directory)::

      "${path}"
        create       => "false",
        perms        => mog("${mode}", "${owner}", "${group}"),
        classes      => classes_generic_two("${uniq_old_class_prefix}", "${uniq_class_prefix}");

    is_type_files.is_target_directory.!is_depth_zero::

      "${path}"
        create       => "false",
        perms        => mog("${mode}", "${owner}", "${group}"),
        depth_search => recurse("${recursion}"),
        file_select  => plain,
        classes      => classes_generic_two("${uniq_old_class_prefix}", "${uniq_class_prefix}");

    is_type_files.((is_target_directory.is_depth_zero)|!is_target_directory)::

      "${path}"
        create       => "false",
        perms        => mog("${mode}", "${owner}", "${group}"),
        file_select  => plain,
        classes      => classes_generic_two("${uniq_old_class_prefix}", "${uniq_class_prefix}");

    is_type_directories.is_target_directory.!is_depth_zero::

      "${path}"
        create       => "false",
        perms        => mog("${mode}", "${owner}", "${group}"),
        depth_search => recurse_with_base("${recursion}"),
        file_select  => dirs,
        classes      => classes_generic_two("${uniq_old_class_prefix}", "${uniq_class_prefix}");

    is_type_directories.is_target_directory.is_depth_zero::

      "${path}"
        create       => "false",
        perms        => mog("${mode}", "${owner}", "${group}"),
        file_select  => dirs,
        classes      => classes_generic_two("${uniq_old_class_prefix}", "${uniq_class_prefix}");

  methods:

    is_type_directories.!is_target_directory::
      "error report if target is a file and we manage directory"
            usebundle => _classes_failure("${uniq_old_class_prefix}");
      "error report if target is a file and we manage directory"
            usebundle => _classes_failure("${uniq_class_prefix}");


    !target_exists::
      "error report if target doesn't exist"
        usebundle => _classes_failure("${uniq_old_class_prefix}");
      "error report if target doesn't exist"
        usebundle => _classes_failure("${uniq_class_prefix}");

    any::
      "old outcome classes" usebundle => _classes_copy("${uniq_class_prefix}", "${class_prefix}");
      "old outcome classes" usebundle => _classes_copy("${uniq_old_class_prefix}", "${old_class_prefix}");
      "report"              usebundle => _log_v3("Ensure permissions mode ${mode}, owner ${owner} and group ${group} on ${path} on type ${type} with ${recursion} recursion level", "${path}", "${uniq_old_class_prefix}", "${uniq_class_prefix}", @{args});
}
