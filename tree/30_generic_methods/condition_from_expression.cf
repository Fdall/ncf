#####################################################################################
# Copyright 2015 Normation SAS
#####################################################################################
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, Version 3.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#
#####################################################################################

# @name Condition from expression
# @description Create a new condition
# @documentation This method evaluates an expression, and produces a `${condition_prefix}_true` or a `${condition_prefix}_false` condition depending on the result on the expression:
#
# * If the expression results in a "defined" state, this will produce a kept outcome class and a `${condition_prefix}_true` condition
# * If the expression results in an "undefined" state, this will produce a kept outcome class and a `${condition_prefix}_false` condition
#
# Calling this method with a condition expression transforms a complex expression into a single condition.
#
# The created condition is global to the agent.
#
# #### Example
#
# If you want to check if a condition evaluates to true, like checking that you are on Monday, 2am, on RedHat systems, you can use the following policy
#
# ```
# condition_from_expression("backup_time", "Monday.redhat.Hr02")
# ```
#
# * If the system is a RedHat like system, on Monday, at 2am, then it will define the following conditions
# `backup_time_true`, `condition_from_expression_backup_time_kept`, `condition_from_expression_backup_time_reached`
# * If the system not a RedHat like system, or it's not Monday, or it's not 2am, then it will define the following conditions
# `backup_time_false`, `condition_from_expression_backup_time_kept`, `condition_from_expression_backup_time_reached`
# * If the condition is invalid (cannot be parsed), it will define only
# `condition_from_expression_backup_time_kept`, `condition_from_expression_backup_time_reached`
#
# @parameter condition_prefix     The condition prefix
# @parameter condition_expression The expression evaluated to create the condition (use 'any' to always evaluate to true)
#
# @class_prefix condition_from_expression
# @class_parameter condition_prefix

bundle agent condition_from_expression(condition_prefix, condition_expression)
{
  vars:
      "old_class_prefix"  string => canonify("condition_from_expression_${condition_prefix}");
      "args"               slist => { "${condition_prefix}", "${condition_expression}" };
      "report_param"      string => join("_", args);
      "full_class_prefix" string => canonify("condition_from_expression_${report_param}");
      "class_prefix"      string => string_head("${full_class_prefix}", "1000");

  classes:
      "${condition_prefix}_true"  expression => "${condition_expression}",
                                       scope => "namespace";

      "${condition_prefix}_false" expression => "!(${condition_expression})",
                                       scope => "namespace";

  methods:
      "success" usebundle => _classes_success("${old_class_prefix}");
      "success" usebundle => _classes_success("${class_prefix}");

      "report_info_true"
        usebundle  => _log_v3_mode("log_info", "Expression ${condition_expression} was valid, ${condition_prefix}_true defined", "${condition_prefix}", "${old_class_prefix}", "${class_prefix}", @{args}),
        ifvarclass => "${condition_prefix}_true";

      "report_info_false"
        usebundle  => _log_v3_mode("log_info", "Expression ${condition_expression} was invalid, ${condition_prefix}_false defined", "${condition_prefix}", "${old_class_prefix}", "${class_prefix}", @{args}),
        ifvarclass => "${condition_prefix}_false";

      "report"  usebundle => _log_v3("Create the condition ${condition_prefix}_{true,false} with ${condition_expression}", "${condition_prefix}", "${old_class_prefix}", "${class_prefix}", @{args});
}
